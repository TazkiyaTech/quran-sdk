plugins {
    id 'com.jfrog.bintray' version '1.6'
}

apply plugin: 'maven-publish'
apply plugin: 'com.android.library'

group = "${projectGroupName}"
version = "${projectVersionName}"

FileCollection proGuardFileCollection = files { file('./proguard').listFiles() }
FileCollection proGuardDebugFileCollection = files { file('./proguard-debug').listFiles() }

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        versionCode Integer.parseInt("${projectVersionCode}")
        versionName "${projectVersionName}"
    }

    dexOptions {
        incremental true
        javaMaxHeapSize "1024M"
    }

    buildTypes {
        debug {
            minifyEnabled true
            useProguard false
            proguardFile(getDefaultProguardFile('proguard-android.txt'))
            proguardFiles(proGuardFileCollection)
            proguardFiles(proGuardDebugFileCollection)
        }

        release {
            minifyEnabled true
            useProguard false
            proguardFile(getDefaultProguardFile('proguard-android.txt'))
            proguardFiles(proGuardFileCollection)
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile 'com.google.guava:guava:19.0'

    testCompile 'junit:junit:4.12'

    androidTestCompile 'com.android.support:support-annotations:23.3.0'
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.android.support.test:rules:0.4.1'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task generateJavadocsJar(type: Jar, dependsOn: generateJavadocs) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

publishing {
    publications {
        QuranSDK(MavenPublication) {
            artifact "$buildDir/outputs/aar/Android-release.aar"
            artifact generateJavadocsJar
            artifact generateSourcesJar

            groupId "${projectGroupName}"
//            artifactId "Android"
            version "${projectVersionName}"

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

bintray {
    user = "${bintray_user}"
    key = "${bintray_api_key}"
    publications = ['QuranSDK']
    pkg {
        repo = 'maven'
        name = 'quran-sdk'
//        userOrg = 'Think In Code' // optional
        licenses = ['Apache-2.0']
        vcsUrl = 'https://bitbucket.org/adilson05uk/quran-sdk.git'

        version {
            name = "${projectVersionName}"
            desc = 'Latest release!'
            released  = new Date()
//            vcsTag = "release_" + "${projectVersionName}"
        }
    }
}
