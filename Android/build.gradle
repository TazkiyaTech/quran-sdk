apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = getProperty('projectGroupName').toString()
version = getProperty('projectVersionName').toString()

File[] proGuardFileCollection = file('./proguard').listFiles()
File[] proGuardDebugFileCollection = file('./proguard-debug').listFiles()

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.1'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError true
    }

    buildTypes {
        debug {
            minifyEnabled true
            useProguard false
            proguardFile(getDefaultProguardFile('proguard-android.txt'))
            proguardFiles(proGuardFileCollection)
            proguardFiles(proGuardDebugFileCollection)
        }

        release {
            minifyEnabled true
            useProguard false
            proguardFile(getDefaultProguardFile('proguard-android.txt'))
            proguardFiles(proGuardFileCollection)
        }
    }
}

dependencies {
    compile 'com.android.support:support-annotations:26.0.2'
    compile 'com.thinkincode.utils:Android:0.2.4'

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'

    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
    group = 'build'
}

task generateJavadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += configurations.compile
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
    group = 'build'
}

task generateJavadocJar(type: Jar) {
    from generateJavadoc
    classifier 'javadoc'
    group = 'build'
}

publishing {
    publications {
        QuranSDK(MavenPublication) {
            artifact "$buildDir/outputs/aar/Android-release.aar"
            artifact generateJavadocJar
            artifact generateSourcesJar

            groupId getProperty('projectGroupName').toString()
//            artifactId "Android"
            version getProperty('projectVersionName').toString()

            //The publication doesn't know about our dependencies, so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

bintray {
    user = getProperty('bintray_user').toString()
    key = getProperty('bintray_api_key').toString()
    publications = ['QuranSDK']
    pkg {
        repo = 'maven'
        name = 'quran-sdk'
//        userOrg = 'Think In Code' // optional
        licenses = ['Apache-2.0']
        vcsUrl = 'https://bitbucket.org/adilson05uk/quran-sdk.git'

        version {
            name = getProperty('projectVersionName').toString()
            desc = 'Latest release!'
            released  = new Date()
            vcsTag = 'release_' + getProperty('projectVersionName').toString()
        }
    }
}
